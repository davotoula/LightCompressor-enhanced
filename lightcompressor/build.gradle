plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'io.github.simonhauck.release' version '1.5.0'
}

// Double check version in gradle.properties and ./gradlew release -PreleaseType=<release-type major/minor/patch>
release {
    checkForPreReleaseVersions = false
    checkForUncommittedFiles = false
}

android {
    compileSdkVersion libs.versions.compileSdk.get().toInteger()
    defaultConfig {
        minSdkVersion libs.versions.minSdk.get().toInteger()
        targetSdkVersion libs.versions.targetSdk.get().toInteger()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    buildFeatures {
        buildConfig = true
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
    namespace = 'com.abedelazizshe.lightcompressorlibrary'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation libs.kotlin.stdlib
    implementation libs.bundles.kotlinx.coroutines

    // AndroidX
    implementation libs.androidx.annotation
    implementation libs.androidx.core.ktx

    // Parsing
    implementation libs.mp4parser

    // Testing
    testImplementation libs.junit
    androidTestImplementation libs.bundles.androidx.testing
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = "com.github.davotoula"
                artifactId = "LightCompressor-enhanced"
                version = libs.versions.library.version.get()
            }
        }
    }
}