plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdkVersion libs.versions.compileSdk.get().toInteger()
    defaultConfig {
        applicationId "com.davotoula.lce"
        minSdkVersion 24
        targetSdkVersion libs.versions.targetSdk.get().toInteger()
        versionCode project.property('appVersionCode').toInteger()
        versionName project.property('appVersionName')
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            // Support for CI/CD signing with GitHub secrets
            if (System.getenv('KEYSTORE_FILE') != null) {
                // CI build with secrets
                storeFile file(System.getenv('KEYSTORE_FILE'))
                storePassword System.getenv('KEYSTORE_PASSWORD')
                keyAlias System.getenv('KEY_ALIAS')
                keyPassword System.getenv('KEY_PASSWORD')
            } else if (project.hasProperty('RELEASE_STORE_FILE')) {
                // Local build with gradle.properties
                storeFile file(project.property('RELEASE_STORE_FILE'))
                storePassword project.property('RELEASE_STORE_PASSWORD')
                keyAlias project.property('RELEASE_KEY_ALIAS')
                keyPassword project.property('RELEASE_KEY_PASSWORD')
            } else {
                // Fallback to debug keystore for local testing
                storeFile file(System.getProperty('user.home') + '/.android/debug.keystore')
                storePassword 'android'
                keyAlias 'androiddebugkey'
                keyPassword 'android'
            }
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }

    namespace = 'com.davotoula.lce'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':lightcompressor')

    // Kotlin
    implementation libs.kotlin.stdlib
    implementation libs.bundles.kotlinx.coroutines

    // AndroidX Core
    implementation libs.androidx.core.ktx
    implementation libs.bundles.androidx.lifecycle

    // Compose
    implementation platform(libs.compose.bom)
    implementation libs.bundles.compose
    implementation libs.navigation.compose
    implementation libs.lifecycle.viewmodel.compose
    implementation libs.lifecycle.runtime.compose
    implementation libs.compose.material.icons.extended
    debugImplementation libs.bundles.compose.debug

    // Media3
    implementation libs.bundles.media3

    // Image Loading
    implementation libs.coil.compose

    // DataStore
    implementation libs.datastore.preferences

    // Firebase
    implementation platform(libs.firebase.bom)
    implementation libs.firebase.analytics
    implementation libs.firebase.crashlytics

    // Testing
    testImplementation libs.junit
    androidTestImplementation libs.bundles.androidx.testing
}
